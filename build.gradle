plugins{
    id 'groovy'
    id 'wrapper'
    id 'java-gradle-plugin'
}

def integrationTest = sourceSets.create("integrationTest")
def defaultGradleVersion = '7.3'
def allGradleVersions = ['7.0', '7.0.1', '7.0.2', '7.1', '7.1.1', 
    '7.2', '7.3', '7.3.1', '7.3.2', '7.3.3', '7.4']
repositories {
    mavenCentral()
}


dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.4.15'
    testImplementation 'junit:junit:4.13'
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.8.2"
    integrationTestImplementation gradleTestKit()
    integrationTestImplementation 'junit:junit:4.13'
    integrationTestRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.8.2"
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        // Uncomment this line if you'd like to accept the terms-of-service automatically
        //  It is not ideal to accept this by default, as downstream users would be agreeing
        //  to ToS they would not be presented to read.
        // Default for this value is "no"
        termsOfServiceAgree = "yes"
        publishAlways()
    }
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'org.boblloyd.GradleProjectReport'
            implementationClass = 'org.boblloyd.gradleprojectreport.ProjectReport'
        }
    }
    testSourceSets sourceSets.integrationTest
}

tasks.register("integrationTest", Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    systemProperty "gradleVersion", defaultGradleVersion
}

tasks.register('integrationTestAll')

allGradleVersions.each{ gradleVersion ->
    tasks.register("integrationTest-${gradleVersion}", Test) {
        description = 'Runs integration tests.'
        group = 'verification'

        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath

        systemProperty "gradleVersion", gradleVersion
    }
    tasks.integrationTestAll.dependsOn("integrationTest-${gradleVersion}")
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

check.dependsOn "integrationTest"

tasks.register('package', Zip){
    from fileTree("."){
        exclude(".gradle")
        exclude(".git")
        exclude("build")
    }
}